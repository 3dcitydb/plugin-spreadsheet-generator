import org.apache.tools.ant.filters.ReplaceTokens

group 'org.citydb'
version '3.0.0'

apply plugin: 'java'
apply plugin: 'distribution'
apply from: 'properties.gradle'

sourceCompatibility = 1.8

repositories {
    maven {
        url 'http://download.osgeo.org/webdav/geotools/'
    }
    maven{
        url 'https://dl.bintray.com/3dcitydb/maven'
    }
    maven {
        url 'https://maven.imagej.net/content/repositories/thirdparty/'
    }
    jcenter()
}

dependencies {
    compile 'org.citydb:impexp-client:4.0.0'
    compile 'org.apache.poi:poi:3.17'
    compile 'org.apache.poi:poi-ooxml:3.17'
    compile 'org.apache.poi:poi-ooxml-schemas:3.17'
    compile 'net.sourceforge.javacsv:javacsv:2.0'
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'resources/license/APACHE-2.0.txt'
        from 'resources/license/LICENSE.txt'
    }
}

distributions.main.contents {
    from jar
    from('resources/doc/README.txt') {
        filter(ReplaceTokens, tokens: [
                pluginName: project.pluginName,
                pluginShortName: project.pluginShortName,
                version: project.version,
                date: project.date.format('yyy-MM-dd'),
                impexpName: project.impexpName,
                impexpVersion: project.impexpVersion,
                pkgName: "$project.name-$project.version".toString(),
                zipName: "$project.name-$project.version".toString() + '.zip',
                citydbName: project.citydbName,
                citydbWebsiteUrl: project.citydbWebsiteUrl,
                citydbVcsUrl: project.citydbVcsUrl,
                pluginUrl: project.pluginUrl,
                vendorName: project.vendorName,
                vendorOrganisation: project.vendorOrganisation,
                vendorCountry: project.vendorCountry,
                vendorCopyright: project.vendorCopyright,
                vendorWebsiteUrl: project.vendorWebsiteUrl
        ])
    }
    into('lib') {
        from configurations.runtime.exclude(group: 'org.citydb')
    }
    into('templates') {
        from 'resources/templates'
    }
    into('license') {
        from 'resources/license/APACHE-2.0.txt'
        from 'resources/license/LICENSE.txt'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.5.1'
}